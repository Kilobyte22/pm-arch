# Maintainer: Bhushan Shah <bshah@kde.org>

pkgname=calamares
pkgver=r1477.8a52661
pkgrel=1
pkgdesc="Distribution Independent installer"
arch=('i686' 'x86_64')
url=https://calamares.io/
license=('GPL')
depends=('qt5-svg' 'qt5-webkit' 'boost-libs' 'parted' 'python' 'yaml-cpp')
source=('git://github.com/calamares/calamares.git'
        'git://github.com/calamares/partitionmanager.git'
        )
md5sums=('SKIP' 'SKIP')

pkgver() {
  cd calamares
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
  cd calamares
  git submodule init
  git config submodule.src/modules/partition/partitionmanager.url "$srcdir"/partitionmanager
  git submodule update src/modules/partition/partitionmanager
}

build() {
  cd build
  cmake ../calamares \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Debug \
    -DWITH_PARTITIONMANAGER=1 \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
