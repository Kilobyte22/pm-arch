# Maintainer: Bhushan Shah <bshah@kde.org>
pkgname=libqofono-qt5
pkgver=0.80.r2.d9a8603
pkgrel=1
pkgdesc="A library for accessing the ofono daemon, and a declarative plugin for it. This allows accessing ofono in qtquick and friends."
arch=('i686' 'x86_64')
url="https://github.com/nemomobile/libqofono"
license=('LGPL')
makedepends=('git')
depends=('qt5-declarative')
provides=("libqofono-qt5")
source=('git://github.com/nemomobile/libqofono.git#tag=0.81')
md5sums=('SKIP')

pkgver() {
	cd libqofono
	printf "%s" "$(git describe --long | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    qmake-qt5 PREFIX=/usr $srcdir/libqofono
	make
}

package() {
    cd build
	make INSTALL_ROOT="$pkgdir/" install
    rm -r "$pkgdir/usr/lib/libqofono-qt5/"
}
