# Maintainer: Bhushan Shah <bhush94 at gmail dot com>
pkgname=plasma-camera-git
pkgver=r9.0a68cd2
pkgrel=1
pkgdesc="Simple camera application for mobile devices"
arch=('any')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL3')
depends=('plasma-framework')
makedepends=('git' 'cmake')
provides=('plasma-camera')
source=('git://anongit.kde.org/plasma-camera.git')
md5sums=('SKIP')

_gitname="plasma-camera"

pkgver() {
  cd plasma-camera
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../plasma-camera \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
